ext {
    preDexLibs = !project.hasProperty('disablePreDex')

    version = '0.4.0'
    versionCode = 12
    minSdkVersion = 14
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.3'
}

def kotlinVersion = '1.1.2-4'
def supportLibraryVersion = '25.3.1'

def retrofitVersion = '2.3.0'
def moshiVersion = '1.5.0'
def kotterknifeVersion = '0.1.0-SNAPSHOT'
def leakCanaryVersion = '1.5.1'

ext.deps = [
        kotlinStdlib       : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        supportAppCompat   : "com.android.support:appcompat-v7:$supportLibraryVersion",
        supportFragment    : "com.android.support:support-fragment:$supportLibraryVersion",
        supportRecyclerView: "com.android.support:recyclerview-v7:$supportLibraryVersion",
        supportCardView    : "com.android.support:cardview-v7:$supportLibraryVersion",

        retrofit           : "com.squareup.retrofit2:retrofit:$retrofitVersion",
        retrofitMoshi      : "com.squareup.retrofit2:converter-moshi:$retrofitVersion",
        moshi              : "com.squareup.moshi:moshi:$moshiVersion",

        kotterknife        : "com.jakewharton:kotterknife:$kotterknifeVersion",

        leakCanary         : "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion",
        leakCanaryNoOp     : "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
]

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.2-4"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.14"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.14"
    }
}

allprojects {
    version = rootProject.ext.version

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
}

subprojects {
    def appPlugin = "com.android.build.gradle.AppPlugin"
    def libraryPlugin = "com.android.build.gradle.LibraryPlugin"

    project.plugins.whenPluginAdded { plugin ->
        if (appPlugin == plugin.class.name || libraryPlugin == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
