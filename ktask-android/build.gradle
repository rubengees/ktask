apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.rubengees'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.version
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile deps.kotlinStdlib
    compile deps.supportFragment

    compile project(':ktask')
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.kotlin.srcDirs
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = file("$buildDir/javadoc")
    sourceDirs = files('src/main/kotlin')
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from file("$buildDir/javadoc")
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
